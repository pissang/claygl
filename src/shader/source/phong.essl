
// http://en.wikipedia.org/wiki/Blinn%E2%80%93Phong_shading_model

@export buildin.phong.vertex

uniform mat4 worldViewProjection : WORLDVIEWPROJECTION;
uniform mat4 worldInverseTranspose : WORLDINVERSETRANSPOSE;
uniform mat4 world : WORLD;

uniform vec2 uvRepeat : [1.0, 1.0];
uniform vec2 uvOffset : [0.0, 0.0];

attribute vec3 position : POSITION;
attribute vec2 texcoord : TEXCOORD_0;
attribute vec3 normal : NORMAL;
attribute vec4 tangent : TANGENT;

#ifdef VERTEX_COLOR
attribute vec4 color : COLOR;
#endif

attribute vec3 barycentric;

#ifdef SKINNING
attribute vec3 weight : WEIGHT;
attribute vec4 joint : JOINT;

uniform mat4 skinMatrix[JOINT_NUMBER] : SKIN_MATRIX;
#endif

varying vec2 v_Texcoord;
varying vec3 v_Normal;
varying vec3 v_WorldPosition;
varying vec3 v_Barycentric;

#ifdef NORMALMAP_ENABLED
varying vec3 v_Tangent;
varying vec3 v_Bitangent;
#endif

#ifdef VERTEX_COLOR
varying vec4 v_Color;
#endif

void main()
{
    
    vec3 skinnedPosition = position;
    vec3 skinnedNormal = normal;
    vec3 skinnedTangent = tangent.xyz;
#ifdef SKINNING
    
    @import buildin.chunk.skin_matrix

    skinnedPosition = (skinMatrixWS * vec4(position, 1.0)).xyz;
    // Upper 3x3 of skinMatrix is orthogonal
    skinnedNormal = (skinMatrixWS * vec4(normal, 0.0)).xyz;
    skinnedTangent = (skinMatrixWS * vec4(tangent.xyz, 0.0)).xyz;
#endif

    gl_Position = worldViewProjection * vec4(skinnedPosition, 1.0);

    v_Texcoord = texcoord * uvRepeat + uvOffset;
    v_WorldPosition = (world * vec4(skinnedPosition, 1.0)).xyz;
    v_Barycentric = barycentric;

    v_Normal = normalize((worldInverseTranspose * vec4(skinnedNormal, 0.0)).xyz);
    
#ifdef NORMALMAP_ENABLED
    v_Tangent = normalize((worldInverseTranspose * vec4(skinnedTangent, 0.0)).xyz);
    v_Bitangent = normalize(cross(v_Normal, v_Tangent) * tangent.w);
#endif

#ifdef VERTEX_COLOR
    v_Color = color;
#endif
}

@end


@export buildin.phong.fragment

uniform mat4 viewInverse : VIEWINVERSE;

varying vec2 v_Texcoord;
varying vec3 v_Normal;
varying vec3 v_WorldPosition;

#ifdef NORMALMAP_ENABLED
varying vec3 v_Tangent;
varying vec3 v_Bitangent;
#endif

uniform sampler2D diffuseMap;
uniform sampler2D normalMap;
uniform sampler2D specularMap;
uniform samplerCube environmentMap;

uniform vec3 color : [1.0, 1.0, 1.0];
uniform float alpha : 1.0;

uniform float shininess : 30;

uniform vec3 specularColor : [1.0, 1.0, 1.0];
uniform vec3 emission : [0.0, 0.0, 0.0];

uniform float reflectivity : 0.5;

// Uniforms for wireframe
uniform float lineWidth : 0.0;
uniform vec3 lineColor : [0.0, 0.0, 0.0];
varying vec3 v_Barycentric;

#ifdef AMBIENT_LIGHT_NUMBER
@import buildin.header.ambient_light
#endif
#ifdef POINT_LIGHT_NUMBER
@import buildin.header.point_light
#endif
#ifdef DIRECTIONAL_LIGHT_NUMBER
@import buildin.header.directional_light
#endif
#ifdef SPOT_LIGHT_NUMBER
@import buildin.header.spot_light
#endif

#extension GL_OES_standard_derivatives : enable
// Import util functions and uniforms needed
@import buildin.util.calculate_attenuation

@import buildin.util.edge_factor

@import buildin.plugin.compute_shadow_map

void main()
{
#ifdef RENDER_TEXCOORD
    gl_FragColor = vec4(v_Texcoord, 1.0, 1.0);
    return;
#endif

    vec4 finalColor = vec4(color, alpha);

    vec3 eyePos = viewInverse[3].xyz;
    vec3 viewDirection = normalize(eyePos - v_WorldPosition);

#ifdef DIFFUSEMAP_ENABLED
    vec4 tex = texture2D(diffuseMap, v_Texcoord);
#ifdef SRGB_DECODE
    tex.rgb = pow(tex.rgb, vec3(2.2));
#endif
    finalColor.rgb *= tex.rgb;
#ifdef DIFFUSEMAP_ALPHA_ALPHA
    finalColor.a *= tex.a;
#endif
#endif

    vec3 spec = specularColor;
    #ifdef SPECULARMAP_ENABLED
        spec *= texture2D(specularMap, v_Texcoord).rgb;
    #endif

    vec3 normal = v_Normal;
#ifdef NORMALMAP_ENABLED
    normal = texture2D(normalMap, v_Texcoord).xyz * 2.0 - 1.0;
    mat3 tbn = mat3(v_Tangent, v_Bitangent, v_Normal);
    normal = normalize(tbn * normal);
#endif

#ifdef RENDER_NORMAL
    gl_FragColor = vec4(normal, 1.0);
    return;
#endif

    // Diffuse part of all lights
    vec3 diffuseTerm = vec3(0.0, 0.0, 0.0);
    // Specular part of all lights
    vec3 specularTerm = vec3(0.0, 0.0, 0.0);
    
#ifdef AMBIENT_LIGHT_NUMBER
    for(int i = 0; i < AMBIENT_LIGHT_NUMBER; i++)
    {
        diffuseTerm += ambientLightColor[i];
    }
#endif
#ifdef POINT_LIGHT_NUMBER
#if defined(POINT_LIGHT_SHADOWMAP_NUMBER)
    float shadowContribsPoint[POINT_LIGHT_NUMBER];
    if(shadowEnabled)
    {
        computeShadowOfPointLights(v_WorldPosition, shadowContribsPoint);
    }
#endif
    for(int i = 0; i < POINT_LIGHT_NUMBER; i++)
    {
        vec3 lightPosition = pointLightPosition[i];
        vec3 lightColor = pointLightColor[i];
        float range = pointLightRange[i];

        vec3 lightDirection = lightPosition - v_WorldPosition;

        // Calculate point light attenuation
        float dist = length(lightDirection);
        float attenuation = lightAttenuation(dist, range); 

        // Normalize vectors
        lightDirection /= dist;
        vec3 halfVector = normalize(lightDirection + viewDirection);

        float ndh = dot(normal, halfVector);
        ndh = clamp(ndh, 0.0, 1.0);

        float ndl = dot(normal,  lightDirection);
        ndl = clamp(ndl, 0.0, 1.0);

        float shadowContrib = 1.0;
#if defined(POINT_LIGHT_SHADOWMAP_NUMBER)
        if(shadowEnabled)
        {
            shadowContrib = shadowContribsPoint[i];
        }
#endif

        vec3 li = lightColor * ndl * attenuation * shadowContrib;

        diffuseTerm += li;
        if (shininess > 0.0)
        {
            specularTerm += li * pow(ndh, shininess);
        }

    }
#endif

#ifdef DIRECTIONAL_LIGHT_NUMBER
#if defined(DIRECTIONAL_LIGHT_SHADOWMAP_NUMBER)
    float shadowContribsDir[DIRECTIONAL_LIGHT_NUMBER];
    if(shadowEnabled)
    {
        computeShadowOfDirectionalLights(v_WorldPosition, shadowContribsDir);
    }
#endif
    for(int i = 0; i < DIRECTIONAL_LIGHT_NUMBER; i++)
    {

        vec3 lightDirection = -normalize(directionalLightDirection[i]);
        vec3 lightColor = directionalLightColor[i];

        vec3 halfVector = normalize(lightDirection + viewDirection);

        float ndh = dot(normal, halfVector);
        ndh = clamp(ndh, 0.0, 1.0);

        float ndl = dot(normal, lightDirection);
        ndl = clamp(ndl, 0.0, 1.0);

        float shadowContrib = 1.0;
#if defined(DIRECTIONAL_LIGHT_SHADOWMAP_NUMBER)
        if(shadowEnabled)
        {
            shadowContrib = shadowContribsDir[i];
        }
#endif

        vec3 li = lightColor * ndl * shadowContrib;

        diffuseTerm += li;
        if (shininess > 0.0)
        {
            specularTerm += li * pow(ndh, shininess);
        }
    }
#endif

#ifdef SPOT_LIGHT_NUMBER
#if defined(SPOT_LIGHT_SHADOWMAP_NUMBER)
    float shadowContribsSpot[SPOT_LIGHT_NUMBER];
    if(shadowEnabled)
    {
        computeShadowOfSpotLights(v_WorldPosition, shadowContribsSpot);
    }
#endif
    for(int i = 0; i < SPOT_LIGHT_NUMBER; i++)
    {
        vec3 lightPosition = spotLightPosition[i];
        vec3 spotLightDirection = -normalize(spotLightDirection[i]);
        vec3 lightColor = spotLightColor[i];
        float range = spotLightRange[i];
        float a = spotLightUmbraAngleCosine[i];
        float b = spotLightPenumbraAngleCosine[i];
        float falloffFactor = spotLightFalloffFactor[i];

        vec3 lightDirection = lightPosition - v_WorldPosition;
        // Calculate attenuation
        float dist = length(lightDirection);
        float attenuation = lightAttenuation(dist, range); 

        // Normalize light direction
        lightDirection /= dist;
        // Calculate spot light fall off
        float c = dot(spotLightDirection, lightDirection);

        float falloff;
        // Fomular from real-time-rendering
        falloff = clamp((c - a) /( b - a), 0.0, 1.0);
        falloff = pow(falloff, falloffFactor);

        vec3 halfVector = normalize(lightDirection + viewDirection);

        float ndh = dot(normal, halfVector);
        ndh = clamp(ndh, 0.0, 1.0);

        float ndl = dot(normal, lightDirection);
        ndl = clamp(ndl, 0.0, 1.0);

        float shadowContrib = 1.0;
#if defined(SPOT_LIGHT_SHADOWMAP_NUMBER)
        if (shadowEnabled)
        {
            shadowContrib = shadowContribsSpot[i];
        }
#endif

        vec3 li = lightColor * ndl * attenuation * (1.0-falloff) * shadowContrib;

        diffuseTerm += li;
        if (shininess > 0.0)
        {
            specularTerm += li * pow(ndh, shininess);
        }
    }
#endif

    finalColor.rgb *= diffuseTerm;
    finalColor.rgb += specularTerm * spec;
    finalColor.rgb += emission;

#ifdef ENVIRONMENTMAP_ENABLED
    vec3 envTex = textureCube(environmentMap, reflect(-viewDirection, normal)).xyz;
    finalColor.rgb = finalColor.rgb + envTex * reflectivity;
#endif

    if(lineWidth > 0.01)
    {
        finalColor.rgb = finalColor.rgb * mix(lineColor, vec3(1.0), edgeFactor(lineWidth));
    }

#ifdef GAMMA_ENCODE
    finalColor.rgb = pow(finalColor.rgb, vec3(1 / 2.2));
#endif

    gl_FragColor = finalColor;
}

@end