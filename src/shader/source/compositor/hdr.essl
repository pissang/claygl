// HDR Pipeline
@export buildin.compositor.hdr.bright

uniform sampler2D texture;
uniform float threshold : 1;
uniform float scale : 1.0;

varying vec2 v_Texcoord;

const vec3 lumWeight = vec3(0.2125, 0.7154, 0.0721);

@import buildin.util.rgbm_decode
@import buildin.util.rgbm_encode

void main()
{
#ifdef TEXTURE_ENABLED
#ifdef RGBM_DECODE
    vec3 tex = RGBMDecode(texture2D(texture, v_Texcoord));
#else
    vec3 tex = texture2D(texture, v_Texcoord).rgb;
#endif
#else
    vec3 tex = vec3(0.0);
#endif

    float lum = dot(tex, lumWeight);
    if (lum > threshold)
    {
        gl_FragColor.rgb = tex * scale;
    }
    else
    {
        gl_FragColor.rgb = vec3(0.0);
    }
    gl_FragColor.a = 1.0;

#ifdef RGBM_ENCODE
    gl_FragColor.rgba = RGBMEncode(gl_FragColor.rgb);
#endif
}
@end

@export buildin.compositor.hdr.log_lum

varying vec2 v_Texcoord;

uniform sampler2D texture;

const vec3 w = vec3(0.2125, 0.7154, 0.0721);

void main()
{
    vec4 tex = texture2D(texture, v_Texcoord);
    float luminance = dot(tex.rgb, w);
    luminance = log(luminance + 0.001);

    gl_FragColor = vec4(vec3(luminance), 1.0);
}

@end

@export buildin.compositor.hdr.lum_adaption
varying vec2 v_Texcoord;

uniform sampler2D adaptedLum;
uniform sampler2D currentLum;

uniform float frameTime : 0.02;

void main()
{
    float fAdaptedLum = texture2D(adaptedLum, vec2(0.5, 0.5)).r;
    float fCurrentLum = exp(texture2D(currentLum, vec2(0.5, 0.5)).r);

    fAdaptedLum += (fCurrentLum - fAdaptedLum) * (1.0 - pow(0.98, 30.0 * frameTime));
    gl_FragColor.rgb = vec3(fAdaptedLum);
    gl_FragColor.a = 1.0;
}
@end

// Tone mapping with gamma correction
// http://filmicgames.com/archives/75
@export buildin.compositor.hdr.tonemapping

uniform sampler2D texture;
uniform sampler2D bloom;
uniform sampler2D lensflare;
uniform sampler2D lum;

uniform float exposure : 1.0;

varying vec2 v_Texcoord;

const float A = 0.22;   // Shoulder Strength
const float B = 0.30;   // Linear Strength
const float C = 0.10;   // Linear Angle
const float D = 0.20;   // Toe Strength
const float E = 0.01;   // Toe Numerator
const float F = 0.30;   // Toe Denominator
const vec3 whiteScale = vec3(11.2);

vec3 uncharted2ToneMap(vec3 x)
{
    return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}

vec3 filmicToneMap(vec3 color)
{
    vec3 x = max(vec3(0.0), color - 0.004);
    return (x*(6.2*x+0.5))/(x*(6.2*x+1.7)+0.06);
}

float eyeAdaption(float fLum)
{
    return mix(0.2, fLum, 0.5);
}

void main()
{
    vec3 tex = vec3(0.0);
    float a = 1.0;
#ifdef TEXTURE_ENABLED
    vec4 res = texture2D(texture, v_Texcoord);
    a = res.a;
    tex = res.rgb;
#endif

#ifdef BLOOM_ENABLED
    tex += texture2D(bloom, v_Texcoord).rgb * 0.25;
#endif

#ifdef LENSFLARE_ENABLED
    tex += texture2D(lensflare, v_Texcoord).rgb;
#endif

// Adjust exposure
// From KlayGE
#ifdef LUM_ENABLED
    float fLum = texture2D(lum, vec2(0.5, 0.5)).r;
    float adaptedLumDest = 3.0 / (max(0.1, 1.0 + 10.0*eyeAdaption(fLum)));
    float exposureBias = adaptedLumDest * exposure;
#else
    float exposureBias = exposure;
#endif
    tex *= exposureBias;

    // Do tone mapping
    vec3 color = uncharted2ToneMap(tex) / uncharted2ToneMap(whiteScale);
    color = pow(color, vec3(1.0/2.2));
    // vec3 color = filmicToneMap(tex);

#ifdef RGBM_ENCODE
    gl_FragColor.rgba = RGBMEncode(color);
#else
    gl_FragColor = vec4(color, a);
#endif
}

@end